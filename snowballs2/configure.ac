dnl ====================================================================
dnl Configuration script for Snowballs 2
dnl ====================================================================
dnl
dnl $Id: configure.in,v 1.16 2002-07-08 15:10:16 lecroart Exp $
dnl 


dnl ====================================================================
dnl Process this file with autoconf to produce a configure script.
dnl ====================================================================

# If you want to change the version, must must change AC_INIT
# *and* AC_SUBST(LIBTOOL_VERSION)

AC_PREREQ(2.57)
AC_INIT([snowballs],[0.2.0],[nel-all@nevrax.org])
AM_INIT_AUTOMAKE([tar-ustar])

AM_CONFIG_HEADER(config.h)

AC_SUBST(LIBTOOL_VERSION, [0:2:0])

dnl ====================================================================
dnl Checks for programs.
dnl ====================================================================
AC_CANONICAL_HOST
AC_PROG_CXX
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_LIBTOOL
AM_PROG_LIBTOOL
AM_SANITY_CHECK

AC_SYS_LARGEFILE

AM_MAINTAINER_MODE

dnl The following hack should ensure that configure doesn't add optimizing
dnl or debugging flags to CFLAGS or CXXFLAGS
CFLAGS="$CFLAGS "
CXXFLAGS="$CXXFLAGS "


dnl ====================================================================
dnl Checks Snowballs modules (Client / Server) to install / Disable
dnl ====================================================================

SNO_SUBDIRS=""

dnl Client
AC_ARG_ENABLE( client,
    [  --disable-client        disable compilation and install of Snowballs Client.],
    [ AC_MSG_RESULT(disable Snowballs Client.) ],
    [ SNO_SUBDIRS="$SNO_SUBDIRS client"
      enable_client="yes" ] )

dnl Server
AC_ARG_ENABLE( server,
    [  --disable-server        disable compilation and install of Snowballs Server.],
    [ AC_MSG_RESULT(disable Snowballs Server.) ],
    [ SNO_SUBDIRS="$SNO_SUBDIRS server"
      enable_server="yes" ] )

AC_SUBST(SNO_SUBDIRS)


dnl ====================================================================
dnl Configure Settings
dnl ====================================================================

dnl Set the C++ compiler as the default compiler for the configure script
dnl compilation tests
AC_LANG_CPLUSPLUS


dnl ====================================================================
dnl Debug/optimized compilation mode
dnl ====================================================================

AM_NEL_DEBUG


dnl ====================================================================
dnl Checks for typedefs, structures, and compiler characteristics.
dnl ====================================================================


dnl ====================================================================
dnl Checks for header files.
dnl ====================================================================

AC_STDC_HEADERS
AC_CHECK_HEADERS(fcntl.h unistd.h)


dnl ====================================================================
dnl X11

AC_PATH_X

if test ! "$no_x" = "yes" ; then
    if test ! X"$x_libraries" = X
    then
        LIBS="$LIBS -L$x_libraries"
    fi
    if test ! X"$x_includes" = X
    then
        CXXFLAGS="$CXXFLAGS -I$x_includes"
    fi
else
    if test "$enable_client" = "yes"
    then
        AC_MSG_ERROR(X11 must be installed.)
    fi
fi


dnl ====================================================================
dnl Checks for libraries.
dnl ====================================================================

dnl ====================================================================
dnl STLPort

AM_PATH_STLPORT


dnl ====================================================================
dnl OpenAL

AM_PATH_OPENAL($enable_client)


dnl ====================================================================
dnl NeL

dnl         misc   net    3d              pacs            sound ai    georges
AM_PATH_NEL("yes", "yes", $enable_client, $enable_client, "no", "no", "no")

dnl ====================================================================
dnl FreeType 2

AM_PATH_FREETYPE($enable_client)

dnl ====================================================================
dnl Checks for library functions.
dnl ====================================================================

dnl ====================================================================
dnl Snowballs data, configuration and logs file location
dnl ====================================================================

test "x$prefix" = xNONE && prefix=$ac_default_prefix

AC_ARG_WITH(database,
    [  --with-database[=DIR]
                          use DIR instead of the default datadir/games/snowballs],
    [database=$withval],
    [database='${datadir}/games/snowballs'])
eval eval SNOWBALLS_DATABASE=\"$database/\"
AC_SUBST(SNOWBALLS_DATABASE)

AC_ARG_WITH(sysconfdir,
    [  --with-sysconfdir[=DIR]
                          use DIR instead of the default sysconfdir/snowballs],
    [pkgsysconfdir=$withval],
    [pkgsysconfdir='${sysconfdir}/snowballs'])
AC_SUBST(pkgsysconfdir)

eval eval SNOWBALLS_CONFIG=\"$pkgsysconfdir/\"
AH_TEMPLATE([SNOWBALLS_CONFIG], [The path to the Snowballs config files.])
AC_DEFINE([SNOWBALLS_CONFIG], "$SNOWBALLS_CONFIG")

AC_ARG_WITH(logdir,
    [  --with-logdir[=DIR]
                          use DIR instead of the default localstatedir/log/snowballs],
    [logdir=$withval],
    [logdir='${localstatedir}/log/snowballs'])
AC_SUBST(logdir)

eval eval SNOWBALLS_LOGS=\"$logdir/\"
AH_TEMPLATE([SNOWBALLS_LOGS], [The path to where Snowballs logs should be stored.])
AC_DEFINE([SNOWBALLS_LOGS], "$SNOWBALLS_LOGS")

dnl ====================================================================
dnl Output files to generate.
dnl ====================================================================

AC_CONFIG_FILES([Makefile                                         \
           client/Makefile                                  \
           client/client.cfg                                \
           client/src/Makefile                              \
           server/Makefile                                  \
           server/frontend/Makefile                         \
           server/frontend/src/Makefile                     \
           server/chat/Makefile                             \
           server/chat/src/Makefile                         \
           server/position/Makefile                         \
           server/position/src/Makefile                     \
])
AC_OUTPUT

dnl End of configure.in

